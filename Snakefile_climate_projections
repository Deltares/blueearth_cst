import itertools
import sys

# read path of the config file to give to the weagen scripts
args = sys.argv
config_path = args[args.index("--configfile") + 1]

# Parsing the Snakemake config file (options for basins to build, data catalog, model output directory)
# Function to get argument from config file and return default value if not found
def get_config(config, arg, default=None, optional=True):
    """
    Function to get argument from config file and return default value if not found

    Parameters
    ----------
    config : dict
        config file
    arg : str   
        argument to get from config file
    default : str/int/float/list, optional
        default value if argument not found, by default None
    optional : bool, optional
        if True, argument is optional, by default True
    """
    if arg in config:
        return config[arg]
    elif optional:
        return default
    else:
        raise ValueError(f"Argument {arg} not found in config file")

project_dir = get_config(config, 'project_dir', optional=False)
DATA_SOURCES = get_config(config, "data_sources_climate", optional=False)

clim_project = get_config(config, "clim_project", optional=False)
models = get_config(config, "models", optional=False)
scenarios = get_config(config, "scenarios", optional=False)
members = get_config(config, "members", optional=False)
variables = get_config(config, "variables", optional=False)

start_month_hyd_year = get_config(config, "start_month_hyd_year", "Jan")
time_horizon_hist = get_config(config, "historical", optional=False)
future_horizons = get_config(config, "future_horizons", optional=False)

save_grids = get_config(config, "save_grids", False)

basin_dir = f"{project_dir}/hydrology_model"
clim_project_dir = f"{project_dir}/climate_projections/{clim_project}"

### Dictionary elements from the config based on wildcards
def get_horizon(wildcards):
    return config["future_horizons"][wildcards.horizon]

# Master rule: end with csv with change factors for each scenario and model
rule all:
    input:
        #ancient(expand((clim_project_dir + "/annual_change_scalar_stats-{model}_{scenario}_{horizon}.nc"), model = config["models"], scenario = config["scenarios"], horizon = config["future_horizons"])),
        (clim_project_dir + "/annual_change_scalar_stats_summary.nc"),
        (clim_project_dir + "/annual_change_scalar_stats_summary.csv"),
        (clim_project_dir + "/annual_change_scalar_stats_summary_mean.csv"),
        stats_change_plt = (clim_project_dir + "/plots/projected_climate_statistics.png"),
        precip_plt = (clim_project_dir + "/plots/precipitation_anomaly_projections.png"),
        temp_plt = (clim_project_dir + "/plots/temperature_anomaly_projections.png"),
        snake_config = f"{project_dir}/config/snake_config_climate_projections.yml",

ruleorder: monthly_stats_hist > monthly_stats_fut > monthly_change > monthly_change_scalar_merge

# Rule to copy config files to the project_dir/config folder
rule copy_config:
    input:
        config_snake = config_path,
    params:
        data_catalogs = DATA_SOURCES,
        workflow_name = "climate_projections",
    output:
        config_snake_out = f"{project_dir}/config/snake_config_climate_projections.yml",
    script:
        "src/copy_config_files.py"

# Rule to calculate mean monthly statistics for historical and future scenarios - grid saved to netcdf
# also calculate monthly time series averaged over the grid.
rule monthly_stats_hist:
    input:
        region_fid = ancient(f"{basin_dir}/staticgeoms/region.geojson"),
    output:
        #stats_nc_hist = (clim_project_dir + "/historical_stats_{model}.nc"),
        stats_time_nc_hist = temp(clim_project_dir + "/historical_stats_time_{model}.nc"),
    params:
        yml_fid = DATA_SOURCES,
        project_dir = f"{project_dir}",
        name_scenario = "historical",
        name_members = members,
        name_model = "{model}",
        name_clim_project = clim_project,
        variables = variables,
        save_grids = save_grids,
    script: "src/get_stats_climate_proj.py"

# Rule to calculate mean monthly statistics for historical and future scenarios - grid saved to netcdf
# also calculate monthly time series averaged over the grid.
rule monthly_stats_fut:
    input:
        region_fid = ancient(f"{basin_dir}/staticgeoms/region.geojson"),
        #stats_nc_hist = ancient(clim_project_dir + "/historical_stats_{model}.nc"), #make sure starts with previous job
        stats_time_nc_hist = (clim_project_dir + "/historical_stats_time_{model}.nc"), #make sure starts with previous job
    output:
        #stats_nc = (clim_project_dir + "/stats-{model}_{scenario}_{horizon}.nc"),
        stats_time_nc = temp(clim_project_dir + "/stats_time-{model}_{scenario}.nc"),
    params:
        yml_fid = DATA_SOURCES,
        project_dir = f"{project_dir}",
        name_scenario = "{scenario}",
        name_members = members,
        name_model = "{model}",
        name_clim_project = clim_project,
        variables = variables,
        save_grids = save_grids,
    script: "src/get_stats_climate_proj.py"

# Rule to calculate change stats over the grid
rule monthly_change:
    input:
        stats_time_nc_hist = ancient(clim_project_dir + "/historical_stats_time_{model}.nc"),
        stats_time_nc = ancient(clim_project_dir + "/stats_time-{model}_{scenario}.nc"),
    output:
        stats_nc_change = temp(clim_project_dir + "/annual_change_scalar_stats-{model}_{scenario}_{horizon}.nc"),
    params:
        clim_project_dir = f"{clim_project_dir}",
        start_month_hyd_year = start_month_hyd_year, 
        name_model = "{model}",
        name_scenario = "{scenario}",
        name_horizon = "{horizon}",
        time_horizon_hist = time_horizon_hist,
        time_horizon_fut = get_horizon,
        save_grids = save_grids,
        stats_nc_hist = (clim_project_dir + "/historical_stats_{model}.nc"),
        stats_nc = (clim_project_dir + "/stats-{model}_{scenario}.nc"),
    script: "src/get_change_climate_proj.py"

#rule to merge results in one netcdf / todo: add plotting
rule monthly_change_scalar_merge:
    input:
        stats_nc_change = ancient(expand((clim_project_dir + "/annual_change_scalar_stats-{model}_{scenario}_{horizon}.nc"), model = models, scenario = scenarios, horizon = future_horizons)),
    output:
        stats_change_summary = (clim_project_dir + "/annual_change_scalar_stats_summary.nc"),
        stats_change_summary_csv = (clim_project_dir + "/annual_change_scalar_stats_summary.csv"),
        stats_change_summary_csv_mean = (clim_project_dir + "/annual_change_scalar_stats_summary_mean.csv"),
        stats_change_plt = (clim_project_dir + "/plots/projected_climate_statistics.png"),
    params:
        clim_project_dir = f"{clim_project_dir}",
        horizons = future_horizons,
    script: "src/get_change_climate_proj_summary.py"

#rule to plot timeseries
rule plot_climate_proj_timeseries:
    input:
        stats_change_summary = (clim_project_dir + "/annual_change_scalar_stats_summary.nc"),
        stats_time_nc_hist =[(clim_project_dir + f"/historical_stats_time_{mod}.nc") for mod in models],
        stats_time_nc = expand((clim_project_dir + "/stats_time-{model}_{scenario}.nc"), model = models, scenario = scenarios),
    params:
        clim_project_dir = f"{clim_project_dir}",
        scenarios = scenarios,
        horizons = future_horizons,
        save_grids = save_grids,
        change_grids = [(clim_project_dir + f"/monthly_change_mean_grid-{mod}_{sc}_{hz}.nc") for mod,sc,hz in list(itertools.product(models,scenarios,future_horizons))],
    output:
        precip_plt = (clim_project_dir + "/plots/precipitation_anomaly_projections.png"),
        temp_plt = (clim_project_dir + "/plots/temperature_anomaly_projections.png"),
        timeseries_csv = (clim_project_dir + "/gcm_timeseries.nc"),
    script: "src/plot_proj_timeseries.py"
