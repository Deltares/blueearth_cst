# Parsing the Snakemake config file (options for basins to build, data catalog, model output directory)
# Function to get argument from config file and return default value if not found
def get_config(config, arg, default=None, optional=True):
    """
    Function to get argument from config file and return default value if not found

    Parameters
    ----------
    config : dict
        config file
    arg : str   
        argument to get from config file
    default : str/int/float/list, optional
        default value if argument not found, by default None
    optional : bool, optional
        if True, argument is optional, by default True
    """
    if arg in config:
        return config[arg]
    elif optional:
        return default
    else:
        raise ValueError(f"Argument {arg} not found in config file")

project_dir = get_config(config, 'project_dir', optional=False)
static_dir = get_config(config, 'static_dir', optional=False)
model_region = get_config(config, 'model_region', optional=False)
model_resolution = get_config(config, 'model_resolution', 0.00833333)
DATA_SOURCES = get_config(config, "data_sources", optional=False)

output_locations = get_config(config, "output_locations", None)
observations_timeseries = get_config(config, "observations_timeseries", None)

wflow_outvars = get_config(config, "wflow_outvars", ['river discharge'])

basin_dir = f"{project_dir}/hydrology_model"

# Master rule: end with all model run and analysed with saving a output plot
rule all:
    input: 
        # f"{project_dir}/plots/wflow_model_performance/hydro_wflow_1.png",
        # f"{project_dir}/plots/wflow_model_performance/basin_area.png",
        # f"{project_dir}/plots/wflow_model_performance/precip.png",
        f"{basin_dir}/run_default/output.csv"

# Rule to build model hydromt build wflow
rule create_model:
    input:
        hydromt_ini = f"{static_dir}/wflow_build_model.ini",
    output:
        basin_nc = f"{basin_dir}/staticmaps.nc",
    container: "singularity/hydromt-wflow.sif"
    shell:
        """hydromt build wflow "{basin_dir}" --region "{model_region}" --opt setup_basemaps.res="{model_resolution}" -i "{input.hydromt_ini}" -d "{DATA_SOURCES}" -vv"""

# Rule to add gauges to the built model
rule add_gauges_and_outputs:
    input:
        basin_nc = f"{basin_dir}/staticmaps.nc"
    output:
        gauges_fid = f"{basin_dir}/staticgeoms/gauges.geojson"
    params:
        output_locs = output_locations,
        outputs = wflow_outvars,
        data_catalog = DATA_SOURCES
    container: "singularity/hydromt-wflow.sif"
    script:
        "src/setup_gauges_and_outputs.py"

# Rule to prepare the time horizon
rule setup_runtime:
    input:
        basin_nc = f"{basin_dir}/staticmaps.nc"
    output:
        forcing_ini = f"{project_dir}/config/wflow_build_forcing_historical.ini"
    params:
        starttime = get_config(config, "starttime", optional=False),
        endtime = get_config(config, "endtime", optional=False),
        clim_source = get_config(config, "clim_historical", optional=False),
    container: "singularity/hydromt-wflow.sif"
    script: "src/setup_time_horizon.py"

# Rule to add forcing to the updated model
rule add_forcing:
    input:
        forcing_ini = f"{project_dir}/config/wflow_build_forcing_historical.ini"
    output:
        forcing_fid = f"{project_dir}/climate_historical/wflow_data/inmaps_historical.nc"
    container: "singularity/hydromt-wflow.sif"
    shell:
        """hydromt update wflow "{basin_dir}" -i "{input.forcing_ini}" -d "{DATA_SOURCES}" -vv"""

# Rule to run the wflow model
rule run_wflow:
    input:
        gauges_fid = ancient(f"{basin_dir}/staticgeoms/gauges.geojson"),
        forcing_fid = f"{project_dir}/climate_historical/wflow_data/inmaps_historical.nc"
    output:
        csv_file = f"{basin_dir}/run_default/output.csv"
    params:
        toml_fid = f"{basin_dir}/wflow_sbm.toml"
    container: "singularity/wflow-cli.sif"
    shell:
        """JULIA_NUM_THREADS=4 /app/create_app/wflow_bundle/bin/wflow_cli {params.toml_fid}"""
        # """ julia --threads 4 -e "using Wflow; Wflow.run()" "{params.toml_fid}" """
        #""" julia --threads 4 -e 'import Pkg; Pkg.add("Wflow"); using Wflow; Wflow.run()' "{params.toml_fid}" """

# Rule to analyse and plot wflow model run results --> final output
rule plot_results:
    input:
        csv_file = f"{basin_dir}/run_default/output.csv",
        script = "src/plot_results.py"
    output: 
        output_png = f"{project_dir}/plots/wflow_model_performance/hydro_wflow_1.png",
    params:
        starttime = get_config(config, "starttime", optional=False),
        endtime = get_config(config, "endtime", optional=False),
        project_dir = f"{project_dir}",
        observations_file = observations_timeseries,
        gauges_output_fid = output_locations,
        outputs = wflow_outvars,
    container: "singularity/hydromt-wflow.sif"
    script: "src/plot_results.py"

rule plot_map:
    input:
        gauges_fid = ancient(f"{basin_dir}/staticgeoms/gauges.geojson")
    output:
        output_map_png = f"{project_dir}/plots/wflow_model_performance/basin_area.png",
    params:
        project_dir = f"{project_dir}",
        output_locations = output_locations,
    container: "singularity/hydromt-wflow.sif"
    script: "src/plot_map.py"

# Rule to plot the forcing on a map
rule plot_forcing:
    input:
        forcing_fid = ancient(f"{project_dir}/climate_historical/wflow_data/inmaps_historical.nc"),
    output:
        output_forcing_map = f"{project_dir}/plots/wflow_model_performance/precip.png",
    params:
        project_dir = f"{project_dir}",
        gauges_fid = ancient(f"{basin_dir}/staticgeoms/gauges.geojson")
    container: "singularity/hydromt-wflow.sif"
    script: "src/plot_map_forcing.py"
